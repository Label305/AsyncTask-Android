apply plugin: 'maven'
apply plugin: 'signing'

def sonatypeRepositoryUrl
if (isRelease == 'true') {
    println 'RELEASE BUILD ' + version
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else {
    version = VERSION_NAME + '-SNAPSHOT'
    println 'DEBUG BUILD ' + version
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
}

group = 'com.label305.stan'

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.artifactId = POM_ARTIFACT_ID

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID1
                            name POM_DEVELOPER_NAME1
                        }

                        developer {
                            id POM_DEVELOPER_ID2
                            name POM_DEVELOPER_NAME2
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isRelease == 'true' && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.allJava
    }

    task androidJavadocsJar(type: Jar) {
        classifier = 'javadoc'
        //basename = artifact_id
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        //basename = artifact_id
        from android.sourceSets.main.allSource
    }

    task coreLibJar(type: Jar) {
        from fileTree(dir: 'build/classes/release').matching { include 'com/nhaarman/listviewanimations/**' }
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
        archives coreLibJar
    }
}